<!doctype html>
<html lang="ja">

  <head>
    <meta charset="utf-8">
    <title>情報科学13</title>
    <link rel="stylesheet" href="./reveal.js-master/dist/reveal.css">
    <link rel="stylesheet" href="./theme/darkblue.css" id="theme">
    <link rel="stylesheet" href="./reveal.js-master/plugin/highlight/monokai.css">
    <style>
      .container{ display: flex; }
      .col{ flex: 1; }
      td.center { text-align:center; }
    </style>

  </head>

  <body>
    <div class="reveal">
      <div class="slides">
	<section>
	  <h1>情報科学13</h1>
	  <h2>12. データ Data</h2>
	  	  <h5>数理情報研究室 濱田龍義</h5>
	  </section>
	
	<section>
	  <h2>データ (Data) とは</h2>
	  <ul>
	    <li class="fragment"></li>
          </ul>
	</section>

<section>
	  <h2>12_01 表を読む</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
Table stats;
void setup() {
  stats = loadTable("ortiz.csv");
  for (int i = 0; i < stats.getRowCount(); i++) {
    // i行目，0列目の整数（年）を取得
    int year = stats.getInt(i, 0);
    // i行目，1列目の整数（本塁打数）を取得
    int homeRuns = stats.getInt(i, 1);
    // i行目，2列目の整数（打点）を取得
    int rbi = stats.getInt(i, 2);
     // i行目，3列目の浮動小数点数（打率）を取得
    float average = stats.getFloat(i, 3);
    println(year, homeRuns, rbi, average); // 出力
  }
}
	    </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_01.html" width=240 height=120></iframe>
	</section>

	<section>
	  <h2>12_02 表からグラフを描く</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
int[] homeRuns;
void setup() {
  size(480, 120);
  Table stats = loadTable("ortiz.csv");
  int rowCount = stats.getRowCount();
  homeRuns = new int[rowCount];
  for (int i = 0; i < homeRuns.length; i++) {
    homeRuns[i] = stats.getInt(i, 1);
  }
}
void draw() {
  background(204);
// グラフのメモリを描く
  stroke(255);
  line(20, 100, 20, 20);
  line(20, 100, 460, 100);
  for (int i = 0; i < homeRuns.length; i++) {
    float x = map(i, 0, homeRuns.length-1, 20, 460);
    line(x, 20, x, 100);
  }
// 本塁打数のデータに基づいて折れ線グラフを描画
  noFill();
  stroke(204, 51, 0);
  beginShape();
  for (int i = 0; i < homeRuns.length; i++) {
    float x = map(i, 0, homeRuns.length-1, 20, 460);
    float y = map(homeRuns[i], 0, 60, 100, 20);
    vertex(x, y);
  }
  endShape();
}
      </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_02.html" width=480 height=120></iframe>
	</section>

	<section>
	  <h2>12_03 29740の町</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
Table cities;
void setup() {
  size(240, 120);
  cities = loadTable("cities.csv", "header");
  stroke(255);
}
void draw() {
  background(0, 26, 51);
  float xoffset = map(mouseX, 0, width, -width*3, -width);
  translate(xoffset, -300);
  scale(10);
  strokeWeight(0.1);
  for (int i = 0; i < cities.getRowCount(); i++) {
    float latitude = cities.getFloat(i, "lat");
    float longitude = cities.getFloat(i, "lng");
    setXY(latitude, longitude);
  }
}
void setXY(float lat, float lng) {
  float x = map(lng, -180, 180, 0, width);
  float y = map(lat, 90, -90, 0, height);
  point(x, y);
}
	    </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_03.html" width=240 height=120></iframe>
	</section>

	<section>
	  <h2>12_04 JSONファイルを読む</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
JSONObject film;
void setup() {
  film = loadJSONObject("film.json");
  String title = film.getString("title");
  String dir = film.getString("director");
  int year = film.getInt("year");
  float rating = film.getFloat("rating");
  println(title + " by " + dir + ", " + year);
  println("Rating: " + rating);
}	      
	    </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_04.html" width=240 height=120></iframe>
	</section>

	<section>
	  <h2>12_05 JSONファイルのデータをビジュアライズ</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
Film[] films;
void setup() {
  size(480, 120);
  JSONArray filmArray = loadJSONArray("films.json");
  films = new Film[filmArray.size()];
  for (int i = 0; i < films.length; i++) {
    JSONObject o = filmArray.getJSONObject(i);
    films[i] = new Film(o);
  }
}
void draw() {
  background(0);
  for (int i = 0; i < films.length; i++) {
    int x = i*32 + 32;
    films[i].display(x, 105);
  }
}
class Film {
  String title;
  String director;
  int year;
  float rating;
  Film(JSONObject f) {
    title = f.getString("title");
    director = f.getString("director");
    year = f.getInt("year");
    rating = f.getFloat("rating");
  }
  void display(int x, int y) {
    float ratingGray = map(rating, 6.5, 8.1, 102, 255);
    pushMatrix();
    translate(x, y);
    rotate(-QUARTER_PI);
    fill(ratingGray);
    text(title, 0, 0);
    popMatrix();
  }
}	      
	    </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_05.html" width=480 height=120></iframe>
	</section>

	<section>
	  <h2>12_06 気温データを抽出する</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
void setup() {
  float temp = getTemp("cincinnati.json");
  println(temp);
}
float getTemp(String fileName) {
  JSONObject weather = loadJSONObject(fileName);
  JSONArray list = weather.getJSONArray("list");
  JSONObject item = list.getJSONObject(0);
  JSONObject main = item.getJSONObject("main");
  float temperature = main.getFloat("temp");
  return temperature;
}
	    </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_06.html" width=240 height=120></iframe>
	</section>

	<section>
	  <h2>12_07 メソッドの連結</h2>
	  <pre>
	    <code class="processing" data-trim data-line-numbers="">
JSONObject weather;
void setup() {
  float temp = getTemp("cincinnati.json");
  println(temp);
}
float getTemp(String fileName) {
  JSONObject weather = loadJSONObject(fileName);
  return weather.getJSONArray("list").getJSONObject(0).getJSONObject("main").getFloat("temp");
}
	    </code>
	  </pre>
	  <iframe src="p5js/GSP2nd/Ex_12_07.html" width=240 height=120></iframe>
	</section>

	<section>
	  <h2>まとめ</h2>
	  <ul>
	    <li class="fragment"></li>
          </ul>
	</section>
      </div>
    </div>


<script src="./reveal.js-master/dist/reveal.js"></script>
<script src="./reveal.js-master/plugin/markdown/markdown.js"></script>
<script src="./reveal.js-master/plugin/highlight/highlight.js"></script>
<script src="./reveal.js-master/plugin/notes/notes.js"></script>
<script src="./reveal.js-master/plugin/math/math.js"></script>

<script>
  Reveal.initialize({
      transition: 'concave',
      controls: true,
      progress: true,
      history: true,
      center: true,
      plugins:
  	[ RevealMarkdown, RevealHighlight, RevealNotes, RevealMath.KaTeX ], 
  });
</script>

</body>
</html>
